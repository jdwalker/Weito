 #created on: Feb 22, 2012
package drools

#list any import classes here.
import pdfbox.TextData
import pdfbox.IFormatData
import pdfbox.FormatStyle
import pdfbox.TextMatch

import java.util.Set
import java.util.HashSet
import java.util.ArrayList
import java.util.List

#declare any global variables here
global ArrayList keywords
global DocumentStatistics stats

declare Wrapper
textdata : TextData @key
end

declare StyledWrapper extends Wrapper
style : FormatStyle
end

declare StyledOriginalWrapper extends StyledWrapper
end


declare Exclude
feature : Feature
end

declare Feature
name : String @key
textdata : TextData @key
wrapper : Wrapper
priority : Integer @key
end

declare Features
name: String @key
list: ArrayList @key
priority : Integer
end

declare FeaturesPriorityTracker
name : String @key
maxpriority: Integer @key
currpriority : Integer @key
end

query "Results"
$feature : DocumentFeatureResult()
end

function ArrayList<StyledWrapper> createMatchStyledWrapper(ArrayList originalWrappers, ArrayList textdatas) {
	ArrayList<StyledWrapper> results = new ArrayList<StyledWrapper>();
	for(Object genObject : textdatas) {
		TextData textdata = (TextData) genObject;
		Wrapper w = new Wrapper( textdata );
		StyledOriginalWrapper maxWrapper = null;
		double overlapRatio = 0;
		for(Object object : originalWrappers) {
			StyledOriginalWrapper ow = (StyledOriginalWrapper) object;
			if ( doWrappersOverlap(w,ow) ) {
				double newOverlapRatio = OverlapRatio(w,ow);
				if(newOverlapRatio > overlapRatio) {
				maxWrapper = ow;
				overlapRatio = newOverlapRatio;
				}
			}
		}
		if(maxWrapper != null) {
			results.add( new StyledWrapper(textdata, maxWrapper.getStyle() ) );
		}
	}
	return results;
}

function boolean doWrappersOverlap(Wrapper firstwrapper, Wrapper secondwrapper) {
	TextData firstTextData = firstwrapper.getTextdata();
	TextData secondTextData = secondwrapper.getTextdata();
	
	int firstTextStart = firstTextData.getStartpos();
	int firstTextEnd = firstTextData.getEndpos();
	
	int secondTextStart = secondTextData.getStartpos();
	int secondTextEnd = secondTextData.getEndpos();
	
	return (firstTextStart >= secondTextStart || firstTextEnd >= secondTextStart) && (secondTextStart >= firstTextStart || secondTextEnd >= firstTextStart);
}

function Double OverlapRatio(Wrapper mainWrapper, Wrapper overlappingWrapper) {
	TextData mainTextData = mainWrapper.getTextdata();
	TextData overlappingTextData = overlappingWrapper.getTextdata();
	
	Double mainTextStart = new Double( mainTextData.getStartpos() );
	Double mainTextEnd = new Double( mainTextData.getEndpos() );
	
	Double overlappingTextStart = new Double( overlappingTextData.getStartpos() );
	Double overlappingTextEnd = new Double( overlappingTextData.getEndpos() );
	
	Double mainLength = mainTextEnd - mainTextStart; // actually the length -1, but the ratios work out in the end
	Double overlappingLength = overlappingTextEnd - overlappingTextStart; // ditto
	Double overlap = (mainLength + overlappingLength - Math.abs(mainTextStart - overlappingTextStart) - Math.abs(mainTextEnd - overlappingTextEnd)) / 2; // overlap amount
	Double ratioOverlap = overlap / mainLength;
	return ratioOverlap;
}


function TextData createTextDataFromFullText(int $startpos, int $endpos, DocumentStatistics stats) {
	int startpos = clampValuestoFullText( $startpos, stats );
	int endpos = clampValuestoFullText( $endpos, stats);
	if(startpos > endpos) {
		endpos = stats.getFulltext().length() - 1;
	}
	String text = stats.getFulltext().substring(startpos,endpos);
	return new TextData(startpos, endpos, text);
}

function int clampValuestoFullText(int value, DocumentStatistics stats) {
		int result = value;
		if( result > (stats.getFulltext().length() - 1) ) result = stats.getFulltext().length() - 1;
		if(result < 0) result = 0;
		return result;
}
/*
function TextData createTextData(TextDataMatcher matcher) {
	return new TextData(matcher.getStartpos(), matcher.getEndpos(), matcher.getText() );
}
*/

function int calculateEndOfSectionFromHeading(Feature f,List headings,DocumentStatistics stats) {
	int endpos = f.getTextdata().getEndpos();
	int endPosOfSection = stats.getFulltext().length() -1;
	for(Object o : headings) {
		Feature heading = (Feature) o;
		int potentialendpos = heading.getTextdata().getStartpos() -1;
		if(potentialendpos > endpos) {
			if(potentialendpos < endPosOfSection) {
				endPosOfSection = potentialendpos;			
			}
		}
	}
	return endPosOfSection;
}

rule "Wrap Format Data"
ruleflow-group "genFormat"
	when
		$cd : IFormatData($text : text, $sp : startpos, $ep : endpos)
		not Wrapper(textdata.text == $text )
	then
		TextData td = new TextData( $sp, $ep, $text );
		StyledOriginalWrapper x = new StyledOriginalWrapper( td, $cd.getPrimarystyle() );
		insert( x );
end

rule "Exclusion of Features"
ruleflow-group "excludeFeatures"
	when
		$e : Exclude($f: feature )
	then
		retract($f);
		retract($e);
end

rule "setup new Features"
ruleflow-group "collectFeatures"
	when
		Feature($name : name, $td : textdata, $priority : priority)
		not Features( name.equals($name), priority == $priority )
	then
		insert( new Features($name, new ArrayList<TextData>(), $priority) );
end

rule "add new Features"
ruleflow-group "collectFeatures"
	when
		$f : Feature($name : name, $td : textdata, $priority : priority)
		$fs : Features( $list : list not contains $td, name.equals($name), priority == $priority )
	then
		$list.add($td);
		modify($fs) {
		setList($list);
		}
end

rule "define Rule Trackers"
ruleflow-group "produceResults"
	when
		Features( $name : name )
		not FeaturesPriorityTracker( name.equals($name) )
		accumulate(Features( name.equals($name), $p : priority ), $maxp : max( $p ), $minp : min($p) )
	then
		insert( new FeaturesPriorityTracker($name,$maxp.intValue(),$minp.intValue()) );
end

rule "Increment Rule Trackers"
ruleflow-group "produceResults"
	when
		$ft : FeaturesPriorityTracker( $name : name, $priority : currpriority, currpriority <= maxpriority )
		not Features(name == $name, priority == $priority ) or Features( list.Empty )
	then
		modify ($ft) {
		setCurrpriority($priority + 1);
		}
		
end

rule "Prepare Results"
ruleflow-group "produceResults"
	when
		$fpt : FeaturesPriorityTracker( $name : name, $priority : currpriority, currpriority <= maxpriority )
		$fs : Features( name == $name, $list : list, priority == $priority, list.Empty == false )
	then
		ArrayList features = new ArrayList();
		for(Object o : $list) {
			TextData td = (TextData) o;
			features.add( td.getText() );
		}
		insert( new DocumentFeatureResult( $name, features ) );
end


rule "Introduction feature"
ruleflow-group "genMetaFeature"
no-loop
	when
		not Feature(name == "introduction", priority == 0)
		accumulate( Feature( name == "heading",  textdata.text matches "(.|\n|\r)*?troduction(.|\n|\r)*?", $sp : textdata.startpos ), $startpos : min ($sp) )
		$f : Feature( name == "heading", textdata.startpos == $startpos.intValue() )
		$headings : ArrayList() from collect (Feature(name == "heading"))
	then
		int endpos = calculateEndOfSectionFromHeading($f,$headings,stats);
		TextData featuretd = createTextDataFromFullText( $f.getTextdata().getStartpos(), endpos, stats);
		insert( featuretd);
		insert( new Feature( "introduction",featuretd, 0) );
end


rule "Introduction feature 1"
ruleflow-group "genMetaFeature"
no-loop
	when
		not Feature(name == "introduction", priority == 1)
		accumulate( Feature( name == "heading",  $sp : textdata.startpos ), $startpos : min ($sp) )
		$f : Feature( name == "heading", textdata.startpos == $startpos.intValue() )
		$headings : ArrayList() from collect (Feature(name == "heading"))
	then
		int endpos = calculateEndOfSectionFromHeading($f,$headings,stats);
		TextData featuretd = createTextDataFromFullText( $f.getTextdata().getStartpos(), endpos, stats);
		insert( featuretd);
		insert( new Feature( "introduction",featuretd, 1) );
	
end


rule "Conclusion feature"
ruleflow-group "genMetaFeature"
no-loop
	when
		not Feature(name == "conclusion", priority == 0)
		$f : Feature( name == "heading",  textdata.text matches "(.|\n|\r)*?(?i)(conclusion|Summary)(.|\n|\r)*?", $sp : textdata.startpos )
		$headings : ArrayList() from collect (Feature(name == "heading"))
	then
		int endpos = calculateEndOfSectionFromHeading($f,$headings,stats);
		TextData featuretd = createTextDataFromFullText( $f.getTextdata().getStartpos(), endpos, stats);
		insert( featuretd);
		insert( new Feature( "conclusion",featuretd, 0) );

end


rule "Conclusion feature 1 - with references"
ruleflow-group "genMetaFeature"
no-loop
	when
		not Feature(name == "conclusion", priority == 1)
		$fr : Feature( name == "references")
		accumulate( Feature( name == "heading",  textdata.text not matches "(.|\n|\r)*?(?i)references(.|\n|\r)*", $sp : textdata.startpos ), $startpos : max ($sp) )
		$f : Feature( name == "heading", textdata.startpos == $startpos.intValue() )
		$headings : ArrayList() from collect (Feature(name == "heading"))
	then
		int endpos = calculateEndOfSectionFromHeading($f,$headings,stats);
		TextData featuretd = createTextDataFromFullText( $f.getTextdata().getStartpos(), endpos, stats);
		insert( featuretd);
		insert( new Feature( "conclusion",featuretd, 1) );
end


rule "Conclusion feature 2 - without references"
ruleflow-group "genMetaFeature"
no-loop
	when
		not Feature(name == "conclusion", priority == 2)
		not Feature( name == "references")
		$fr : Feature( name == "references")
		accumulate( Feature( name == "heading", $sp : textdata.startpos ), $startpos : max ($sp) )
		$f : Feature( name == "heading", textdata.startpos == $startpos.intValue() )
		$headings : ArrayList() from collect (Feature(name == "heading"))
	then
		int endpos = calculateEndOfSectionFromHeading($f,$headings,stats);
		TextData featuretd = createTextDataFromFullText( $f.getTextdata().getStartpos(), endpos, stats);
		insert( featuretd);
		insert( new Feature( "conclusion",featuretd, 2) );
end

rule "References feature"
ruleflow-group "genMetaFeature"
no-loop
	when
		not Feature(name == "references", priority == 0)
		accumulate( Feature( name == "heading",  textdata.text matches "(.|\n|\r)*?(?i)references(.|\n|\r)*", $sp : textdata.startpos ), $startpos : max ($sp) )
		$f : Feature( name == "heading", textdata.startpos == $startpos.intValue() )
		$headings : ArrayList() from collect (Feature(name == "heading"))
	then
		int endpos = calculateEndOfSectionFromHeading($f,$headings,stats);
		TextData featuretd = createTextDataFromFullText( $f.getTextdata().getStartpos(), endpos, stats);
		insert( featuretd);
		insert( new Feature( "references",featuretd, 0) );
end
